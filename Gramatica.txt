<program> ::= <s> <cmd-list> <s> | <s>
<s> ::= " " <s> | "\n" <s> | "\t" <s> | ""
<cmd-list> ::= <cmd> <s> <cmd-list> | <cmd>
<cmd> ::= <cmd-assign> | <cmd-call> | <declare> | <fork> | <function> | <loop> | <break>
<cmd-assign> ::= <assign> <s> ";"
<cmd-call> ::= <call> <s> ";"
<declare> ::= <type> <s> <declare-list> <s> ";"
<fork> ::= <fork-head> <s> <fork-body> <s> "."
<function> ::= <function-args> | <function-no-args>
<loop> ::= "@loop" <s> <cmd-list> <s> "."
<break> ::= "@break" <s> <int> <s> ";" | "@break" <s> ";"
<assign> ::= <l-value> <s> "<--" <s> <r-value>
<call> ::= <id> <s> "(" <s> <call-args> <s> ")" | <id> <s> "(" <s> ")"
<type> ::= <array-type> | <primitive-type>
<declare-list> ::= <declare-item> <s> "," <s> <declare-list> | <declare-item>
<fork-head> ::= "(" <s> <r-value> <s> ")" <s> "?"
<fork-body> ::= <case-true> | <case-false> | <case-true> <s> <case-false>
<function-args> ::= <type> <s> <id> <s> "(" <s> <args> <s> ")" <s> <cmd-list> <s> "."
<function-no-args> ::= <type> <s> <id> <s> "(" <s> ")" <s> <cmd-list> <s> "."
<int> ::= <digit> <int> | <digit>
<l-value> ::= <index-access> | <id> | "@return"
<r-value> ::= <assign> | <expr-1>
<id> ::= <id-head> <id-body> | <id-head>
<call-args> ::= <r-value> <s> "," <s> <call-args> | <r-value>
<array-type> ::= <primitive-type> <s> "[" <s> <int> <s> "]"
<primitive-type> ::= "@int" | "@real" | "@byte"
<declare-item> ::= <id> <s> "<--" <s> <r-value> | <id>
<case-true> ::= "@true" <s> ":" <s> <cmd-list>
<case-false> ::= "@false" <s> ":" <s> <cmd-list>
<args> ::= <type> <s> <id> <s> "," <s> <args> | <type> <s> <id>
<digit> ::= "0" | "1" | ... | "9"
<index-access> ::= <id> <s> "[" <s> <r-value> <s> "]"
<expr-1> ::= <expr-1> <s> <opr-1> <s> <expr-2> | <expr-2>
<id-head> ::= <letter> | "_"
<id-body> ::= <id-body-chr> | <id-body-chr> <id-body>
<opr-1> ::= "|"
<expr-2> ::= <expr-2> <s> <opr-2> <s> <expr-3> | <expr-3>
<letter> ::= "A" | "B" | ... | "Z" | "a" | "b" | ... | "z"
<id-body-chr> ::= <id-head> | <digit>
<opr-2> ::= "&"
<expr-3> ::= <expr-3> <s> <opr-3> <s> <expr-4> | <expr-4>
<opr-3> ::= "<=" | ">=" | "!=" | "=" | "<" | ">"
<expr-4> ::= <expr-4> <s> <opr-4> <s> <expr-5> | <expr-5>
<opr-4> ::= "+" | "-"
<expr-5> ::= <expr-5> <s> <opr-5> <s> <expr-6> | <expr-6>
<opr-5> ::= "*" | "/" | "%"
<expr-6> ::= <expr-7> | <prefix> <s> <expr-7>
<expr-7> ::= "(" <s> <r-value> <s> ")" | <call> | <l-value> | <constant>
<prefix> ::= "'" | "-"
<constant> ::= <string> | <number> | <boolean>
<string> ::= "\"" <string-content> "\""
<number> ::= <decimal> | <int>
<boolean> ::= "@true" | "@false"
<string-content> ::= <text-char> <string-content> | ""
<decimal> ::= <int> "." <int>
<text-char> ::= " " | "!" | "#" | "$" | "%" | "&" | ...
